/**
 * Official Documentation:
 * https://github.com/idnow/de.idnow.android
 * https://github.com/idnow/de.idnow.android-sample
 *
 * 3rd. Party Implementations:
 * https://github.com/mrFunkyWisdom/auto-ident-expo/blob/main/android/build.gradle
 */

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

group = 'com.jtec.idnow'
version = '0.1.0'

def expoModulesCorePlugin = new File(project(":expo-modules-core").projectDir.absolutePath, "ExpoModulesCorePlugin.gradle")
if (expoModulesCorePlugin.exists()) {
    apply from: expoModulesCorePlugin
    applyKotlinExpoModulesCorePlugin()
    useCoreDependencies()
    useExpoPublishing()
}

buildscript {
    // Overrides dependency versions declared by this library.
    ext.safeExtGet = { prop, fallback ->
        rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
    }

    // Overrides Kotlin version and the Kotlin Gradle plugin version.
    ext.getKotlinVersion = {
        if (ext.has("kotlinVersion")) {
            ext.kotlinVersion()
        } else {
            ext.safeExtGet("kotlinVersion", "1.9.25")
        }
    }

    // Overrides IDnow SDK version.
    ext.getIDnowVersion = {
        if (ext.has("idnowVersion")) {
            ext.idnowVersion()
        } else {
            ext.safeExtGet("idnowVersion", "8.4.0")
        }
    }

    repositories {
        mavenCentral()
        google()
        maven {
            url 'https://www.jitpack.io'
        }
        maven {
            url "https://raw.githubusercontent.com/idnow/de.idnow.android/master"
        }
    }

    dependencies {
        classpath("com.android.tools.build:gradle:8.5.2")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${getKotlinVersion()}")
    }
}

android {
    compileSdk safeExtGet("compileSdk", 34)

    // Use NDK version >23 which has both M1 support and is the side-by-side NDK version from AGP.
    ndkVersion safeExtGet("ndkVersion", "26.1.10909125")

    def agpVersion = com.android.Version.ANDROID_GRADLE_PLUGIN_VERSION
    if (agpVersion.tokenize('.')[0].toInteger() < 8) {
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_11
            targetCompatibility JavaVersion.VERSION_11
        }
        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_11.majorVersion
        }
    }

    namespace "com.jtec.idnow"

    defaultConfig {
        minSdkVersion safeExtGet("minSdkVersion", 24)
        targetSdkVersion safeExtGet("targetSdkVersion", 34)

        versionCode 1
        versionName "0.1.0"

        renderscriptTargetApi 24
        renderscriptSupportModeEnabled true
        vectorDrawables.useSupportLibrary = true
    }

    packagingOptions {
        pickFirst 'lib/armeabi-v7a/libRSSupport.so'
    }

    lintOptions {
        abortOnError false
    }
}

allprojects {
    repositories {
        mavenCentral()
        google()
        maven {
            url 'https://www.jitpack.io'
        }
        maven {
            url "https://raw.githubusercontent.com/idnow/de.idnow.android/master"
        }
    }
}

dependencies {
    implementation project(':expo-modules-core')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${getKotlinVersion()}"
    implementation "de.idnow.sdk:idnow-android-sdk:${getIDnowVersion()}"
}
