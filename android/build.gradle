/**
 * Official Documentation:
 * https://github.com/idnow/de.idnow.android
 * https://github.com/idnow/de.idnow.android-sample
 *
 * 3rd. Party Implementations:
 * https://github.com/mrFunkyWisdom/auto-ident-expo/blob/main/android/build.gradle
 */

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

group = 'com.jtec.idnow'
version = '0.1.0'

def expoModulesCorePlugin = new File(project(":expo-modules-core").projectDir.absolutePath, "ExpoModulesCorePlugin.gradle")
if (expoModulesCorePlugin.exists()) {
    apply from: expoModulesCorePlugin
    applyKotlinExpoModulesCorePlugin()
    useCoreDependencies()
    useExpoPublishing()
}

buildscript {
    ext.safeExtGet = { prop, fallback ->
        rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
    }

    ext.getKotlinVersion = {
        if (ext.has("kotlinVersion")) {
            ext.kotlinVersion()
        } else {
            ext.safeExtGet("kotlinVersion", "1.9.23")
        }
    }

    ext.getIDnowVersion = {
        if (ext.has("idnowVersion")) {
            ext.idnowVersion()
        } else {
            ext.safeExtGet("idnowVersion", "8.4.0")
        }
    }
}

android {
    namespace "com.jtec.idnow"

    compileSdk safeExtGet("compileSdkVersion", 34)
    // Use NDK version >23 which has both M1 support and is the side-by-side NDK version from AGP.
    ndkVersion safeExtGet("ndkVersion", "23.1.7779620")

    defaultConfig {
        versionCode 1
        versionName "0.1.0"
        minSdkVersion safeExtGet("minSdkVersion", 23)
        targetSdkVersion safeExtGet("targetSdkVersion", 34)
    }

    def agpVersion = com.android.Version.ANDROID_GRADLE_PLUGIN_VERSION
    if (agpVersion.tokenize('.')[0].toInteger() < 8) {
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_11
            targetCompatibility JavaVersion.VERSION_11
        }
        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_11.majorVersion
        }
    }

    lintOptions {
        abortOnError false
    }
}

allprojects {
    repositories {
        mavenCentral()
        google()
        maven {
            url 'https://www.jitpack.io'
        }
        maven {
            url 'https://raw.githubusercontent.com/idnow/de.idnow.android/master'
        }
        jcenter() {
            // JCenter is now read-only. Therefore, no new versions are published there any more.
            // We only fetch the necessary dependencies for IDnow from JCenter to avoid loading old dependencies.
            content {
                includeModule("com.github.barteksc", "android-pdf-viewer")
            }
        }
    }
}

dependencies {
    implementation project(':expo-modules-core')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${getKotlinVersion()}"
    implementation "de.idnow.sdk:idnow-android-sdk:${getIDnowVersion()}"
}
